# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-magnum-extras
pkgver=dev
pkgrel=1
pkgdesc="Extras for the Magnum C++11/C++14 graphics engine (Emscripten)"
arch=('any')
url="http://magnum.graphics"
license=('MIT')
depends=('emscripten-magnum')
makedepends=('cmake' 'emscripten' 'corrade' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten" ] ; then
        mkdir "$_rootdir/build-emscripten"
        cd "$_rootdir/build-emscripten"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DMAGNUM_DEPLOY_PREFIX=/srv/http/emscripten \
        -DWITH_PLAYER=OFF \
        -DWITH_UI=OFF \
        -DWITH_UI_GALLERY=OFF \
        -DBUILD_TESTS=OFF
    ninja
}

check() {
    cd "$_rootdir/build-emscripten"

    # So tests are not even built (takes AGES) when the user doesn't want to
    cmake . -DBUILD_TESTS=ON
    ninja

    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten"
    DESTDIR="$pkgdir/" ninja install
}
