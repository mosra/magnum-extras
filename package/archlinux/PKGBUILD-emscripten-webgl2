# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-magnum-extras
pkgver=dev.webgl2
pkgrel=1
pkgdesc="Extras for the Magnum C++11/C++14 graphics engine (Emscripten, WebGL 2.0)"
arch=('any')
url="http://magnum.graphics"
license=('MIT')
depends=('emscripten-magnum=dev.webgl2' 'emscripten-magnum-plugins=dev.webgl2')
makedepends=('cmake' 'emscripten' 'corrade' 'ninja')
options=('!strip' '!buildflags')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten-webgl2" ] ; then
        mkdir "$_rootdir/build-emscripten-webgl2"
        cd "$_rootdir/build-emscripten-webgl2"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -DCMAKE_PREFIX_PATH=/usr/lib/emscripten/system \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten-webgl2"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DMAGNUM_DEPLOY_PREFIX=/srv/http/emscripten \
        -DWITH_PLAYER=ON \
        -DWITH_UI=ON \
        -DWITH_UI_GALLERY=ON \
        -DBUILD_TESTS=OFF
    ninja
}

check() {
    cd "$_rootdir/build-emscripten-webgl2"

    # So tests are not even built (takes AGES) when the user doesn't want to
    cmake . -DBUILD_TESTS=ON
    ninja

    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten-webgl2"
    DESTDIR="$pkgdir/" ninja install
}
