# Author: mosra <mosra@centrum.cz>
pkgname=mingw-w64-magnum-extras
pkgver=dev
pkgrel=1
pkgdesc="Extras for the Magnum C++11/C++14 graphics engine (mingw-w64)"
arch=('any')
url="https://magnum.graphics"
license=('MIT')
depends=('mingw-w64-crt' 'mingw-w64-magnum')
makedepends=('mingw-w64-gcc' 'cmake' 'corrade' 'ninja')
options=('!buildflags' '!strip' 'staticlibs')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-mingw-w64-32"
    cd "$_rootdir/build-mingw-w64-32"

    cmake .. \
        -DCMAKE_TOOLCHAIN_FILE=../toolchains/archlinux/basic-mingw-w64-32.cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/i686-w64-mingw32 \
        -DMAGNUM_BUILD_TESTS=ON \
        -DMAGNUM_BUILD_GL_TESTS=ON \
        -DMAGNUM_WITH_UI=ON \
        -DMAGNUM_WITH_UI_GALLERY=ON \
        -G Ninja
    ninja

    mkdir -p "$_rootdir/build-mingw-w64-64"
    cd "$_rootdir/build-mingw-w64-64"

    cmake .. \
        -DCMAKE_TOOLCHAIN_FILE=../toolchains/archlinux/basic-mingw-w64-64.cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/x86_64-w64-mingw32 \
        -DMAGNUM_BUILD_TESTS=ON \
        -DMAGNUM_BUILD_GL_TESTS=ON \
        -DMAGNUM_WITH_PLAYER=ON \
        -DMAGNUM_WITH_UI=ON \
        -DMAGNUM_WITH_UI_GALLERY=ON \
        -G Ninja
    ninja
}

package() {
  cd "$_rootdir/build-mingw-w64-32"
  DESTDIR="$pkgdir/" ninja install/strip

  cd "$_rootdir/build-mingw-w64-64"
  DESTDIR="$pkgdir/" ninja install/strip
}
