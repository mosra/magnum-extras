# Author: mosra <mosra@centrum.cz>
pkgname=magnum-extras
pkgver=dev
pkgrel=1
pkgdesc="Extras for the Magnum C++11/C++14 graphics engine (coverage build)"
arch=('i686' 'x86_64')
url="https://magnum.graphics"
license=('MIT')
depends=('magnum' 'magnum-plugins')
makedepends=('cmake' 'ninja' 'lcov')
options=(!strip)
provides=('magnum-extras-git')

_rootdir=$startdir/../../

build() {
    mkdir -p "$_rootdir/build-coverage"
    cd "$_rootdir/build-coverage"

    # Colored output is enabled implicitly. If Ninja detects it's outputting to
    # a pipe, it strips it away from the output, alternatively you can set the
    # GCC_COLORS= (empty) env variable to temporarily disable colors. The
    # inverse, i.e. preserving colors when Ninja outputs to a pipe can be done
    # with CLICOLOR_FORCE=1: https://github.com/ninja-build/ninja/issues/2196
    cmake .. \
        -DCMAKE_CXX_FLAGS="--coverage" \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_COLOR_DIAGNOSTICS=ON \
        -DMAGNUM_BUILD_TESTS=ON \
        -DMAGNUM_BUILD_GL_TESTS=ON \
        -DMAGNUM_WITH_PLAYER=ON \
        -DMAGNUM_WITH_UI=ON \
        -DMAGNUM_WITH_UI_GALLERY=ON \
        -G Ninja
    ninja
}

check() {
    cd "$_rootdir/build-coverage"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure -j9 || true
    # Keep in sync with PKGBUILD, see additional comments there
    CORRADE_TEST_COLOR=ON MAGNUM_DISABLE_EXTENSIONS="GL_ARB_multi_bind GL_ARB_shading_language_420pack GL_ARB_explicit_uniform_location" ctest --output-on-failure -j9 -R GLTest || true
    CORRADE_TEST_COLOR=ON MAGNUM_DISABLE_EXTENSIONS="GL_ARB_direct_state_access GL_ARB_robustness" ctest --output-on-failure -j9 -R GLTest || true

    rm -rf coverage
    mkdir coverage
    # Keep in sync with package/ci/appveyor-lcov.sh,
    # appveyor-desktop{,-mingw}.bat and circleci.yml, please
    # TODO figure out a way to avoid adding --ignore-errors mismatch etc
    lcov --ignore-errors mismatch,inconsistent --directory . --capture --output-file coverage.info
    lcov --ignore-errors inconsistent --extract coverage.info "*/src/Magnum/*" --output-file coverage.info
    lcov --ignore-errors inconsistent --remove coverage.info "*/Test/*" --output-file coverage.info
    lcov --ignore-errors inconsistent --remove coverage.info "*/build-coverage/src/Magnum/*" --output-file coverage.info
    genhtml --no-function-coverage --missed --output-directory ./coverage coverage.info
}

package() {
    echo "Open file://$_rootdir/build-coverage/coverage/index.html to see the results." && false
}
